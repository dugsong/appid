dnl Process this file with autoconf to produce a configure script.

AC_INIT(appid.h.tmpl)
AM_INIT_AUTOMAKE(appid, 1.0)
AM_MAINTAINER_MODE

AC_PROG_CC
AM_PROG_LIBTOOL

dnl Checks for Python.
dnl XXX - use AM_PATH_PYTHON after automake upgrade
AC_MSG_CHECKING(for Python)
AC_ARG_WITH(python,
[  --with-python=DIR       build Python module (from optional build directory DIR, or use PATH) ],
[ case "$withval" in
  yes)
     AC_MSG_RESULT(yes)
     PYTHON="python"
     ;;
  no)
     AC_MSG_RESULT(no)
     ;;
  *)
     AC_MSG_RESULT($withval)
     for subdir in . bin; do
        if test -x $withval/$subdir/python; then
           owd=`pwd`
           if cd $withval/$subdir; then withval=`pwd`; cd $owd; fi
           PYTHON="$withval/python"
           break
        fi
     done
     if test "x$PYTHON" = "x"; then
        AC_ERROR(python not found in $withval)
     fi
     ;;
  esac 
],
[ AC_MSG_RESULT(no) ])
AC_SUBST(PYTHON)
AM_CONDITIONAL(PYTHON, [test "x$PYTHON" != "x"])

dnl Checks for Ragel.
AC_MSG_CHECKING(for Ragel)
AC_ARG_WITH(ragel,
[  --with-ragel=DIR        use Ragel to compile new appid.c (from optional build directory DIR, or use PATH) ],
[ case "$withval" in
  yes)
     AC_MSG_RESULT(yes)
     RAGEL="ragel"
     RLGEN_CD="rlgen-cd"
     ;;
  no)
     AC_MSG_RESULT(no)
     ;;
  *)
     AC_MSG_RESULT($withval)
     if test -x $withval/ragel/ragel && test -x $withval/rlgen-cd/rlgen-cd; then
        RAGEL="$withval/ragel/ragel"
	RLGEN_CD="$withval/rlgen-cd/rlgen-cd"
     fi
     if test "x$RAGEL" = "x"; then
        AC_ERROR(ragel or rlgen-cd not found in $withval)
     fi
     ;;
  esac 
],
[ AC_MSG_RESULT(no) ])
AC_SUBST(RAGEL)
AC_SUBST(RLGEN_CD)
AM_CONDITIONAL(RAGEL, [test "x$RAGEL" != "x"])

AC_OUTPUT([ Makefile ])
